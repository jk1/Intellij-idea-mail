apply plugin: 'kotlin'

configurations {
    ideaAPI
    libs
    compile.extendsFrom ideaAPI, libs
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

sourceCompatibility = JavaVersion.VERSION_1_7
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    libs 'org.apache.james:apache-mime4j-dom:0.7.2'
    libs 'org.apache.mina:mina-core:2.0.7'
    libs 'javax.mail:mail:1.4.7'
    libs 'org.slf4j:slf4j-api:1.7.5'
    libs "org.jetbrains.kotlin:kotlin-stdlib:$ext.kotlin_version"
    libs files('lib/subethasmtp_authpatch_gwc-3.1.7.1.jar')

    ideaAPI files('lib/openapi.jar')
    ideaAPI files('lib/annotations.jar')
    ideaAPI files('lib/util.jar')
    ideaAPI files('lib/extensions.jar')

    testCompile 'junit:junit:4.+'
}

buildscript {
    ext.kotlin_version = '0.6.1673'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$ext.kotlin_version"
    }
}


task dist(dependsOn: jar, type: Zip) {
    into('intellij-mail/lib') {
        from jar.outputs.files, configurations.libs
    }
    archiveName = 'intellij-mail.zip'
    destinationDir buildDir
}